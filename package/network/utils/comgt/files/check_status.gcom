opengt
set com 115200n81
set senddelay 0.05
waitquiet 1 0.2
flash 0.1
:start
	print "check status..\n"
	send "AT+CSQ^m"
	get 2 "^m" $s
	get 2 ":" $s
	get 2 "," $s
	print "csq=",$s,"\n"
	send "AT+CCSQ^m"
	get 2 "^m" $s
	get 2 ":" $s
	get 2 "," $s
	print "ccsq=",$s,"\n"
	send "AT\^HDRCSQ^m"
	get 2 "^m" $s
	get 2 ":" $s
	get 2 "^m" $s
	print "hdrcsq=",$s,"\n"
:cont
	send "AT+COPS=3,0^m"
	get 2 "" $s
	waitquiet 1 0.2
	send "AT+COPS?^m"
	get 2 "^m" $s
	get 2 "\"" $s
	get 2 "\"" $s
	print "COPS=",$s,"\n"
	let c=0
:nettype
	waitquiet 1 0.2
	send "AT+PSRAT^m"
	get 2 "^m" $s
	get 2 "^m" $s
	let a=len($s)
	let b=a-9
	if b < 1 goto net_ec20
	let $c=$right($s,b)
	print "netstatus=",$c,"\n"
	goto iccid
:net_ec20
	waitquiet 1 0.2
	send "AT+QNWINFO^m"
	get 2 "^m" $s
	get 2 "\"" $s
	get 2 "\"" $s
	get 2 "^m" $t
	get 2 "\"" $t
	get 2 "\"" $t
	let a=len($t)
	if a < 1 goto firstone
	print "netstatus=",$t,"\n"
	goto iccid
:firstone
	let a=len($s)
	if a < 1 goto netnull
	print "netstatus=",$s,"\n"
	goto iccid
:netnull
	print "netstatus=NONE\n"
:iccid
	waitquiet 1 0.2
	send "AT+ICCID^m"
	get 2 "^m" $s
	get 2 "^m" $s
	let a=len($s)
	let b=a-8
	if b < 1 goto iccerr
	let $c=$right($s,b)
	print "ICCID=",$c,"\n"
	let c=0
:sysinfo
   send "AT\^SYSINFO^m"
   get 2 "^m" $s
   get 2 ":" $s
   get 2 "^m" $s
   print "SYSINFO=",$s,"\n"
   goto out
:iccerr
   print "ICCID=error\n"
:out
	exit 0
